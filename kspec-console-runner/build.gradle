import org.apache.tools.ant.filters.ReplaceTokens

apply from: "$rootDir/gradle/common/dependencies.gradle"
apply from: "$rootDir/gradle/common/kotlin.gradle"

apply plugin: "application"

dokka {
    outputFormat = 'html'
}

mainClassName = "io.polymorphicpanda.kspec.console.MainKt"

sourceSets {
    spec {
    }
}

startScripts {
    applicationName = "kspec"
}

dependencies {
    compile project(":kspec-launcher")
    compile 'net.sf.jopt-simple:jopt-simple'
    compile "org.jetbrains.kotlin:kotlin-compiler"
    compile "org.slf4j:slf4j-api"

    runtime "ch.qos.logback:logback-classic"

    testCompile "junit:junit"
    testCompile "com.natpryce:hamkrest"

    specCompile project(":kspec-core")
}

task sourceJar(type: Jar) {
    from project.sourceSets.main.allSource
    classifier "sources"
}

processResources {
    filter ReplaceTokens, tokens: [
        "kspecVersion": version,
        "kotlinVersion": kotlinVersion
    ]
}


processTestResources {
    afterEvaluate {
        from(sourceSets.spec.allSource) {
            into "specs_raw"
        }
    }
}
